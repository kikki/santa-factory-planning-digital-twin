InternalClassType: Method
Coordinate3D: [6.5,-0.30000000000000004,0]
Name: StartWith_Indep_Frequ
Origin: 37f8d1a4-be02-4cb6-9d28-54bb247ac89e
Program: |+1
 -- first valid solution, generated by the independent frequencies
 -- called by: Gradient
 param sol:table->boolean
 
 var k,tryNo,row,n,j:integer;var h,r,f,lowBound:real;var ok,debugging:boolean;var str:string
 var ApplObj:object
 
 ApplObj := rootfolder.BasicObjects.&Meth.WizardObj -- object link
 debugging := ApplObj.debug_Mode
 if debugging AND numRestart > 0  
 	print "Restart ",numRestart," with independent frequencies as a start value"
 end
 n := xTable.xDim-1-- number of independent x
 lowBound := -1e-15
 
 repeat
 	tryNo := tryNo + 1
 	for k := 1 to n 
 		str := xTable[k,0]
 		j := frequencies.getRowNo(str)
 		f := frequencies[1,j]-- independent frequency as a start value
 		if f>0 
 			repeat
 				r := f * z_uniform(1, 0.95, 1.05)
 			until (r>0) AND (r<1)
 		else
 			r:=0
 		end
 		sol[1,k] := r -- frequency
 	next
 	
 	row := 0; ok := true
 	while row < xTable.yDim AND ok 
 		row := row + 1 -- row in 'xTable'
 		r := 0
 		for k := 1 to n 
 			r := r + sol[1,k] * xTable[k,row]
 		next -- k
 		r := r + xTable[n+1,row]-- dependent x
 		ok := ok AND (r >= lowBound) AND (r<=1)
 	end-- row of xTable
 	
 until ok OR tryNo > 1000
 
 --if NOT ok AND debugging then debug;end;
 return ok
 -- of method
RandomSeed: 43
UUID: 7f135c92-e737-46ed-901a-b8ce6495e504
$SequenceNumber: 24
